use super::*;
use log::warn;
use std::str::FromStr;

grammar;

Delimiter<T, D>: Vec<T> = {
    <mut v:(<T> D)*> <e:T?> => match e {
        None => v,
        Some(e) => {
            v.push(e);
            v
        }
    }
};

pub Program: Program = Delimiter<Expr, ";"> => Program(<>);

Exprs: Vec<Expr> = Delimiter<Expr, ",">;

FuncName: (String, String) = {
    <i:Id> => (String::default(), i),
    <ns:Id> "." <name:Id> => (ns, name),
}

UnaryOp: UnaryOp = {
    "+" => UnaryOp::Positive,
    "-" => UnaryOp::Negative,
    "!" => UnaryOp::Not,
}

pub Expr: Expr = {
    <r:Ref> <o:AssignOp> <e:Expr2> => Expr::Binary(Box::new(Expr::Ref(r)), o, Box::new(e)),
    Expr2,
}

Expr2: Expr = {
    <le:Expr2> "||" <re:Expr3> => Expr::Binary(Box::new(le), BinaryOp::Logic(LogicBinaryOp::Or), Box::new(re)),
    Expr3,
}

Expr3: Expr = {
    <le:Expr3> "&&" <re:Expr4> => Expr::Binary(Box::new(le), BinaryOp::Logic(LogicBinaryOp::And), Box::new(re)),
    Expr4,
}

Expr4: Expr = {
    <le:Expr4> "|" <re:Expr5> => Expr::Binary(Box::new(le), BinaryOp::Val(ValBinaryOp::Or), Box::new(re)),
    Expr5,
}

Expr5: Expr = {
    <le:Expr5> "^" <re:Expr6> => Expr::Binary(Box::new(le), BinaryOp::Val(ValBinaryOp::Xor), Box::new(re)),
    Expr6,
}

Expr6: Expr = {
    <le:Expr6> "&" <re:Expr7> => Expr::Binary(Box::new(le), BinaryOp::Val(ValBinaryOp::And), Box::new(re)),
    Expr7,
}

Expr7: Expr = {
    <le:Expr7> "==" <re:Expr8> => Expr::Binary(Box::new(le), BinaryOp::Logic(LogicBinaryOp::Eq), Box::new(re)),
    <le:Expr7> "!=" <re:Expr8> => Expr::Binary(Box::new(le), BinaryOp::Logic(LogicBinaryOp::Neq), Box::new(re)),
    Expr8
}

Expr8: Expr = {
    <le:Expr8> "<" <re:Expr9> => Expr::Binary(Box::new(le), BinaryOp::Logic(LogicBinaryOp::Lt), Box::new(re)),
    <le:Expr8> "<=" <re:Expr9> => Expr::Binary(Box::new(le), BinaryOp::Logic(LogicBinaryOp::Le), Box::new(re)),
    <le:Expr8> ">" <re:Expr9> => Expr::Binary(Box::new(le), BinaryOp::Logic(LogicBinaryOp::Gt), Box::new(re)),
    <le:Expr8> ">=" <re:Expr9> => Expr::Binary(Box::new(le), BinaryOp::Logic(LogicBinaryOp::Ge), Box::new(re)),
    Expr9
}

Expr9: Expr = {
    <le:Expr9> "+" <re:Expr10> => Expr::Binary(Box::new(le), BinaryOp::Val(ValBinaryOp::Add), Box::new(re)),
    <le:Expr9> "-" <re:Expr10> => Expr::Binary(Box::new(le), BinaryOp::Val(ValBinaryOp::Minus), Box::new(re)),
    Expr10
}

Expr10: Expr = {
    <le:Expr10> "*" <re:Expr11> => Expr::Binary(Box::new(le), BinaryOp::Val(ValBinaryOp::Mul), Box::new(re)),
    <le:Expr10> "/" <re:Expr11> => Expr::Binary(Box::new(le), BinaryOp::Val(ValBinaryOp::Div), Box::new(re)),
    <le:Expr10> "%" <re:Expr11> => Expr::Binary(Box::new(le), BinaryOp::Val(ValBinaryOp::Mod), Box::new(re)),
    Expr11
}

Expr11: Expr = {
    <r:Ref> => Expr::Ref(r),
    <c:Const> => Expr::Const(c),
    <o:UnaryOp> <e:Expr11> => Expr::Unary(o, Box::new(e)),
    "(" <e:Expr> ")" => e,
    <i:FuncName> "(" <a:Exprs> ")" => Expr::Call(i.0, i.1, a),
}

AssignOp: BinaryOp = {
    "=" => BinaryOp::Assign,
    "+=" => BinaryOp::Inplace(ValBinaryOp::Add),
    "-=" => BinaryOp::Inplace(ValBinaryOp::Minus),
    "*=" => BinaryOp::Inplace(ValBinaryOp::Mul),
    "/=" => BinaryOp::Inplace(ValBinaryOp::Div),
    "%=" => BinaryOp::Inplace(ValBinaryOp::Mod),
    "&=" => BinaryOp::Inplace(ValBinaryOp::And),
    "|=" => BinaryOp::Inplace(ValBinaryOp::Or),
    "^=" => BinaryOp::Inplace(ValBinaryOp::Xor),
}

pub Ref: Ref = {
    <i:Id> => Ref::Var(i),
    "$" <i:Id> => Ref::Ctx(i),
    "#" <i:Id> => Ref::Res(i),
}

Id: String = <s:r"[A-Za-z]\w*"> => s.into();

pub Const: RawValue = {
    "~" => RawValue::Unit,
    <b:Bool> => RawValue::Bool(b),
    <n:Num> => RawValue::Num(n),
    <s:Str> => RawValue::Str(s),
}

Bool: bool = {
    "true" => true,
    "false" => false,
}

Num: i64 = <s:r"[0-9]+"> => i64::from_str(s).unwrap_or_else(|e| {
    warn!("{}", e);
    0
});

Str: String = <s:r##""[^"\\]*(\\.[^"\\]*)*""##> => s[1..s.len() - 1].into();
